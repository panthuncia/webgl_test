<!DOCTYPE html>
<html>
<head>
    <title>WebGL playground</title>
</head>
<script id="primaryVSSource" type="x-shader/x-vertex">

    in vec3 a_position;
    in vec3 a_normal;
    in vec2 a_texCoord;
    in vec3 a_baryCoord;
    #if defined USE_NORMAL_MAP || defined USE_PARALLAX
        in vec3 a_tangent;
        in vec3 a_bitangent;
    #endif

    uniform mat4 u_modelMatrix;
    uniform mat4 u_viewMatrix;
    uniform mat4 u_modelViewMatrix;
    uniform mat4 u_projectionMatrix;
    uniform mat3 u_normalMatrix;

    out vec3 v_normal;
    out vec4 v_fragPos;
    out vec2 v_texCoord;
    out vec3 v_baryCoord;
    out mat4 m_viewMatrix;
    #if defined USE_NORMAL_MAP || defined USE_PARALLAX
        out mat3 m_TBN;
        out mat3 m_TBNInverse;
    #endif

    void main() {
        // Transform the position into view space
        v_fragPos = vec4(u_modelMatrix * vec4(a_position, 1.0));
    
        // Transform the normal
        // Normal matrix is calculated as inverse transpose
        // of model-view matrix to modify normals appropriately when object is scaled
        v_normal = normalize(u_normalMatrix * a_normal);

    
        // Pass texcoord to fs
        v_texCoord = a_texCoord;

        // Pass barycentric coords for wireframe
        v_baryCoord = a_baryCoord;
    
        m_viewMatrix = u_viewMatrix;
        // Calculate TBN matrix, for transforming tangent-space coordinates to view space
        // Used in normal mapping
        #if defined USE_NORMAL_MAP || defined USE_PARALLAX
            vec3 T = normalize(u_normalMatrix*a_tangent);
            vec3 B = normalize(u_normalMatrix*a_bitangent);
            vec3 N = normalize(u_normalMatrix*a_normal);
            m_TBN = mat3(T, B, N);
            m_TBNInverse = transpose(m_TBN);
        #endif
    
        // Set the position
        gl_Position = u_projectionMatrix * u_viewMatrix * v_fragPos;

    }
</script>

<script id="primaryFSSource" type="x-shader/x-fragment">
    #define PI 3.1415926538
    precision highp sampler2DArray;

    // If we're not using normal mapping, 
    // we want the view-space normals from the vertex shader
    in vec3 v_normal;

    in vec4 v_fragPos;
    in vec2 v_texCoord;  // Received from vertex shader
    in vec3 v_baryCoord;
    in mat4 m_viewMatrix;
    #if defined USE_NORMAL_MAP || defined USE_PARALLAX
        in mat3 m_TBN; // Received from vertex shader
        in mat3 m_TBNInverse;
    #endif

    // Ambient lighting needs to be scaled differently for PBR and non-PBR materials, or shadows look wierd
    #ifdef USE_PBR 
        #define AMBIENT_SCALING_FACTOR 0.2
    #else
        #define AMBIENT_SCALING_FACTOR 15.0
    #endif

    #define LIGHT_TYPE_POINT 0
    #define LIGHT_TYPE_SPOT 1
    #define LIGHT_TYPE_DIRECTIONAL 2


    layout(std140) uniform PerFrame {
        uniform vec4 u_camPosWorldSpace;
        uniform mat4 u_viewMatrixInverse;
    };

    layout(std140) uniform PerMaterial {
        uniform float u_ambientStrength;
        uniform float u_specularStrength;
        uniform float u_textureScale;
        #ifdef USE_PBR
            uniform float u_metallicFactor;
            uniform float u_roughnessFactor;
            uniform vec4 u_baseColorFactor;
        #endif
    };

    layout(std140) uniform LightInfo {
        // Light attributes: x=type (0=point, 1=spot, 2=directional)
        // x=point -> w = shadow caster
        // x=spot -> y= inner cone angle, z= outer cone angle, w= shadow caster
        // x=directional => w= shadow caster
        uniform vec4 u_lightProperties[MAX_LIGHTS];
        uniform vec4 u_lightPosWorldSpace[MAX_LIGHTS]; // Position of the lights
        uniform vec4 u_lightDirWorldSpace[MAX_LIGHTS]; // Direction of the lights
        uniform vec4 u_lightAttenuation[MAX_LIGHTS]; // x,y,z = constant, linear, quadratic attenuation, w= max range
        uniform vec4 u_lightColor[MAX_LIGHTS]; // Color of the lights

        uniform mat4 u_lightSpaceMatrices[MAX_SPOT_LIGHTS]; // for transforming fragments to light-space for shadow sampling
        uniform mat4 u_lightCascadeMatrices[NUM_CASCADE_SPLITS * MAX_DIRECTIONAL_LIGHTS];
        uniform mat4 u_lightCubemapMatrices[6*MAX_POINT_LIGHTS];
        
        uniform float u_cascadeSplits[NUM_CASCADE_SPLITS];
        uniform int u_numLights;
    };

    uniform sampler2DArray u_shadowMaps;
    uniform sampler2DArray u_shadowCascades;
    uniform sampler2DArray u_shadowCubemaps; // Emulated cubemaps (+X, -X, +Y, -Y, +Z, -Z) because OpenGL ES 3.0 doesn't have TEXTURE_CUBE_MAP_ARRAY :/

    uniform sampler2D u_baseColorTexture;
    #ifdef USE_NORMAL_MAP
        uniform sampler2D u_normalMap;
    #endif
    #ifdef USE_BAKED_AO
        uniform sampler2D u_aoMap;
    #endif
    #ifdef USE_PARALLAX
        uniform sampler2D u_heightMap;
    #endif
    #ifdef PBR_MAPS
        #ifdef COMBINED_METALLIC_ROUGHNESS
            uniform sampler2D u_metallicRoughness;
        #else
            uniform sampler2D u_metallic;
            uniform sampler2D u_roughness;
        #endif
    #endif
    #ifdef USE_OPACITY_MAP
        uniform sampler2D u_opacity;
    #endif

    out vec4 fragmentColor;

    // Basic POM
    #ifdef USE_PARALLAX
    vec2 getParallaxCoords(vec2 uv, vec3 viewDir) {
        viewDir = m_TBNInverse*viewDir;
        float heightScale = 0.05;
        int steps = 400;
        float layerDepth = 1.0 / float(steps);
        float currentLayerDepth = 0.0;
        vec2 deltaUV = viewDir.xy * heightScale / (viewDir.z * float(steps));
        vec2 currentUV = uv;

        float currentHeight = texture(u_heightMap, currentUV).r;

        // Loop through the layers to find the intersection point
        for(int i = 0; i < steps; i++) {
            currentLayerDepth += layerDepth;
            currentUV += deltaUV;
            currentHeight = texture(u_heightMap, currentUV).r;

            // If the current depth is greater than the height, we've found the intersection
            if(currentHeight < currentLayerDepth) {
                break;
            }
        }

        // Refine the final UV coordinates using interpolation
        float weight = (currentLayerDepth - currentHeight) / layerDepth;
        vec2 finalUV = currentUV + deltaUV.yx * weight;

        return finalUV;
    }

    // Parallax shadowing, very expensive method (per-fragment*per-light tangent-space raycast)
    float getParallaxShadow(vec2 uv, vec3 lightDir, vec3 viewDir, float sampleHeight){
        lightDir = m_TBNInverse*lightDir;
        int steps = 64;
        float maxDistance = 0.1;
        float currentHeight = texture(u_heightMap, uv).r;
        vec2 lightDirUV = normalize(lightDir.xy);
        float heightStep = lightDir.z / float(steps);
        float stepSizeUV = maxDistance / float(steps);

        for (int i = 0; i < steps; ++i) {
            uv += lightDirUV * stepSizeUV; // Step across
            currentHeight += heightStep; // Step up
            
            float heightAtSample = texture(u_heightMap, uv).r;
    
            if (heightAtSample > currentHeight) {
                return 0.1;
            }
        }
    
        return 1.0;
    }
    // Contact-refinement parallax 
    // https://www.artstation.com/blogs/andreariccardi/3VPo/a-new-approach-for-parallax-mapping-presenting-the-contact-refinement-parallax-mapping-technique
    vec3 getContactRefinementParallaxCoordsAndHeight(vec2 uv, vec3 viewDir){

        // Get view direction in tangent space
        viewDir = m_TBNInverse*viewDir;

        //TODO: move to material definition
        float maxHeight = 0.05;
        float minHeight = maxHeight*0.5;

        // Probably unecessarily high
        int numSteps = 64;
        // Corrects for Z view angle
        float viewCorrection = (-viewDir.z) + 2.0;
        float stepSize = 1.0 / (float(numSteps) + 1.0);
        vec2 stepOffset = viewDir.xy * vec2(maxHeight, maxHeight) * stepSize;

        vec2 lastOffset = viewDir.xy * vec2(minHeight, minHeight) + uv;
        float lastRayDepth = 1.0;
        float lastHeight = 1.0;

        vec2 p1;
        vec2 p2;
        bool refine = false;

        while (numSteps > 0)
        {
            // Advance ray in direction of TS view direction
            vec2 candidateOffset = mod(lastOffset-stepOffset, 1.0);
            float currentRayDepth = lastRayDepth - stepSize;

            // Sample height map at this offset
            float currentHeight = texture(u_heightMap, candidateOffset).r;
            currentHeight = viewCorrection * currentHeight;
            // Test our candidate depth
            if (currentHeight > currentRayDepth)
            {
                p1 = vec2(currentRayDepth, currentHeight);
                p2 = vec2(lastRayDepth, lastHeight);
                // Break if this is the contact refinement pass
                if (refine) {
                    lastHeight = currentHeight;
                    break;
                // Else, continue raycasting with squared precision
                } else {
                    refine = true;
                    lastRayDepth = p2.x;
                    stepSize /= float(numSteps);
                    stepOffset /= float(numSteps);
                    continue;
                }
            }
            lastOffset = candidateOffset;
            lastRayDepth = currentRayDepth;
            lastHeight = currentHeight;
            numSteps -= 1;
        }
        // Interpolate between final two points
        float diff1 = p1.x - p1.y;
        float diff2 = p2.x - p2.y;
        float denominator = diff2 - diff1;

        float parallaxAmount;
        if(denominator != 0.0) {
            parallaxAmount = (p1.x * diff2 - p2.x * diff1) / denominator;
        }

        float offset = ((1.0 - parallaxAmount) * -maxHeight) + minHeight;
        return vec3(viewDir.xy * offset + uv, lastHeight);
    }
    #endif

    // Models spotlight falloff with linear interpolation between inner and outer cone angles
    float spotAttenuation(vec3 pointToLight, vec3 lightDirection, float outerConeCos, float innerConeCos) {
        float cos = dot(normalize(lightDirection), normalize(-pointToLight));
        if(cos > outerConeCos) {
            if(cos < innerConeCos) {
                return smoothstep(outerConeCos, innerConeCos, cos);
            }
            return 1.0;
        }
        return 0.0;
    }

    // http://graphicrants.blogspot.com/2013/08/specular-brdf-reference.html
    // http://ix.cs.uoregon.edu/~hank/441/lectures/pbr_slides.pdf
    // https://learnopengl.com/PBR/Theory
    // Most of this is "plug-and-chug", because I'm not deriving my own BRDF or Fresnel equations

    // Approximates the percent of microfacets in a surface aligned with the halfway vector
    float TrowbridgeReitzGGX(vec3 normalDir, vec3 halfwayDir, float roughness) {
        // UE4 uses alpha = roughness^2, so I will too.
        float alpha = roughness * roughness;
        float alpha2 = alpha* alpha;
        
        float normDotHalf = max(dot(normalDir, halfwayDir), 0.0);
        float normDotHalf2 = normDotHalf * normDotHalf;

        float denom1 = (normDotHalf2 * (alpha2 - 1.0) + 1.0);
        float denom2 = denom1 * denom1;

        return alpha2 / (PI * denom2);
    }
    // Approximates self-shadowing of microfacets on a surface
    float geometrySchlickGGX(float normDotView, float roughness) {
        float r = (roughness + 1.0);
        float k = (r * r) / 8.0;

        float denominator = normDotView * (1.0 - k) + k;
        return normDotView / denominator;
    }
    float geometrySmith(vec3 normalDir, vec3 viewDir, float roughness, float normDotLight) {
        float normDotView = max(dot(normalDir, viewDir), 0.0);

        // combination of shadowing from microfacets obstructing view vector, and microfacets obstructing light vector
        return geometrySchlickGGX(normDotView, roughness) * geometrySchlickGGX(normDotLight, roughness);
    }
    // models increased reflectivity as view angle approaches 90 degrees
    vec3 fresnelSchlick(float cosTheta, vec3 F0) {
        return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
    }

    vec3 calculateLightContribution(int lightIndex, vec3 fragPos, vec3 viewDir, vec3 normal, vec2 uv, vec3 albedo, float metallic, float roughness, vec3 F0, float height) {
        int lightType = int(u_lightProperties[lightIndex].x);
        vec3 lightPos = u_lightPosWorldSpace[lightIndex].xyz;
        vec3 lightColor = u_lightColor[lightIndex].xyz;

        vec3 dir = u_lightDirWorldSpace[lightIndex].xyz;
        
        float constantAttenuation = u_lightAttenuation[lightIndex].x;
        float linearAttenuation = u_lightAttenuation[lightIndex].y;
        float quadraticAttenuation = u_lightAttenuation[lightIndex].z;
        
        float outerConeCos = u_lightProperties[lightIndex].z;
        float innerConeCos = u_lightProperties[lightIndex].y;

        vec3 lightDir;
        float distance;
        float attenuation;
        float spotAttenuationFactor = 0.0;

        // For directional lights, use light dir directly, with zero attenuation
        if(lightType == 2) {
            lightDir = dir;
            attenuation = 1.0;
        } else {
            lightDir = normalize(lightPos - fragPos);
            distance = length(lightPos - fragPos);
            attenuation = 1.0 / (constantAttenuation + linearAttenuation * distance + quadraticAttenuation * distance * distance);
        }

        // Unit vector halfway between view dir and light dir. Makes more accurate specular highlights.
        vec3 halfwayDir = normalize(lightDir + viewDir);

        // PBR lighting
        // https://learnopengl.com/PBR/Theory
        #ifdef USE_PBR
        vec3 radiance = lightColor * attenuation;
        float normDotLight = max(dot(normal, lightDir), 0.0);

        // Cook-Torrence specular BRDF: fCookTorrance=DFG/(4(ωo⋅n)(ωi⋅n))
        // Approximate microfacet alignment
        float normalDistributionFunction = TrowbridgeReitzGGX(normal, halfwayDir, roughness);
        // Approximate microfacet shadowing
        float G = geometrySmith(normal, viewDir, roughness, normDotLight);
        // Approximate specular intensity based on view angle
        vec3 kSpecular = fresnelSchlick(max(dot(halfwayDir, viewDir), 0.0), F0); // F

        // Preserve energy, diffuse+specular must be at most 1.0
        vec3 kDiffuse = vec3(1.0) - kSpecular;
        // Metallic surfaces have no diffuse color
        // model as diffuse color decreases as metallic fudge-factor increases 
        kDiffuse *= 1.0 - metallic;

        vec3 numerator = normalDistributionFunction * G * kSpecular;
        float denominator = 4.0 * max(dot(normal, viewDir), 0.0) * max(dot(normal, lightDir), 0.0) + 0.0001; //+0.0001 fudge-factor to prevent division by 0
        vec3 specular = numerator / denominator;

        vec3 lighting = (kDiffuse * albedo / PI + specular) * radiance * normDotLight;

        #else //Non-PBR lighting

        // Calculate diffuse light
        float diff = max(dot(normal, lightDir), 0.0);
        vec3 diffuse = diff * lightColor;

        // Calculate specular light, Blinn-Phong
        float spec = pow(max(dot(normal, halfwayDir), 0.0), 32.0);
        vec3 specular = u_specularStrength * spec * lightColor;

        // Attenuate
        vec3 lighting = (diffuse + specular) * attenuation;
        #endif

        // For spotlights, apply extra attenuation based on the angle
        if(lightType == 1) {
            float spot = spotAttenuation(lightDir, dir, outerConeCos, innerConeCos);
            lighting *= spot;
        }

        #ifdef USE_PARALLAX
            float parallaxShadow = getParallaxShadow(uv, lightDir, viewDir, height);
            lighting *= parallaxShadow;
        #endif

        return lighting;
    }

    int calculateShadowCascadeIndex(float depth) {
        for(int i = 0; i < NUM_CASCADE_SPLITS; i++) {
            if(depth < u_cascadeSplits[i]) {
                return i;
            }
        }
        return NUM_CASCADE_SPLITS - 1;
    }

    float calculateCascadedShadow(vec4 fragPosWorldSpace, int dirLightNum, vec3 normal, int lightIndex) {
        int cascadeIndex = calculateShadowCascadeIndex(abs(v_fragPos.z)); //abs because -z is forwards
        int infoIndex = NUM_CASCADE_SPLITS*dirLightNum+cascadeIndex;
        vec4 fragPosLightSpace = u_lightCascadeMatrices[infoIndex] * fragPosWorldSpace;
        vec3 uv = fragPosLightSpace.xyz; /// fragPosLightSpace.w;
        uv = uv * 0.5 + 0.5; // Map to [0, 1]
            // Because OpenGL ES lacks CLAMP_TO_BORDER...
        bool isOutside = uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0 || uv.z > 1.0;
        float shadow = 0.0;
        // Kind of a hack, not quite sure why I'm getting texcoords outside of bounds on fragments within the split distance
        // This just steps up one cascade if that happens.
        if (isOutside && cascadeIndex!=NUM_CASCADE_SPLITS-1){
            cascadeIndex+=1;
            infoIndex = NUM_CASCADE_SPLITS*dirLightNum+cascadeIndex;
            fragPosLightSpace = u_lightCascadeMatrices[infoIndex] * fragPosWorldSpace;
            uv = fragPosLightSpace.xyz / fragPosLightSpace.w;
            uv = uv * 0.5 + 0.5; // Map to [0, 1]
            isOutside = false;
        } 
        if(!isOutside) {
            float closestDepth = texture(u_shadowCascades, vec3(uv.xy, float(infoIndex))).r;
            float currentDepth = uv.z;

            //float cosTheta = abs(dot(normal, u_lightDirViewSpace[lightIndex].xyz));
            float bias = 0.0002;
            //float slopeScaledBias = 0.0000;
            //float bias = max(constantBias, slopeScaledBias*cosTheta);
            shadow = currentDepth - bias > closestDepth ? 1.0 : 0.0;
        }
        return shadow;
    }

    float calculateSpotShadow(vec4 fragPosWorldSpace, int spotLightNum){
        vec4 fragPosLightSpace = u_lightSpaceMatrices[spotLightNum] * fragPosWorldSpace;
        vec3 uv = fragPosLightSpace.xyz/fragPosLightSpace.w;
        uv = uv * 0.5 + 0.5; // Map to [0, 1]
        // Because OpenGL ES lacks CLAMP_TO_BORDER...
        bool isOutside = uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0 || uv.z > 1.0;
        float shadow = 0.0;
        if(!isOutside) {
            float closestDepth = texture(u_shadowMaps, vec3(uv.xy, float(spotLightNum))).r;
            float currentDepth = uv.z;

            //float cosTheta = abs(dot(normal, u_lightDirViewSpace[lightIndex].xyz));
            float bias = 0.0002;
            //float slopeScaledBias = 0.0000;
            //float bias = max(constantBias, slopeScaledBias*cosTheta);
            shadow = currentDepth - bias > closestDepth ? 1.0 : 0.0;
        }
        return shadow;
    }

    float calculatePointShadow(vec4 fragPosWorldSpace, int pointLightNum, int lightIndex){
        vec4 lightPosWorldSpace = u_viewMatrixInverse*vec4(u_lightPosWorldSpace[lightIndex].xyz, 1);
        vec3 dir = fragPosWorldSpace.xyz - lightPosWorldSpace.xyz;
        int faceIndex = 0;
        float maxDir = max(max(abs(dir.x), abs(dir.y)), abs(dir.z));

        if (dir.x == maxDir) {
            faceIndex = 0; // +X
        } else if (dir.x == -maxDir) {
            faceIndex = 1; // -X
        } else if (dir.y == maxDir) {
            faceIndex = 2; // +Y
        } else if (dir.y == -maxDir) {
            faceIndex = 3; // -Y
        } else if (dir.z == maxDir) {
            faceIndex = 4; // +Z
        } else if (dir.z == -maxDir) {
            faceIndex = 5; // -Z
        }
        vec4 fragPosLightSpace = u_lightCubemapMatrices[pointLightNum*6+faceIndex]*fragPosWorldSpace;
        vec3 uv = fragPosLightSpace.xyz/fragPosLightSpace.w;
        uv = uv * 0.5 + 0.5;
        bool isOutside = uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0 || uv.z > 1.0;
        float shadow = 0.0;
        if(!isOutside) {
        int layer = pointLightNum * 6 + faceIndex;
            float closestDepth = texture(u_shadowCubemaps, vec3(uv.xy, float(layer))).r;
            float currentDepth = uv.z;
            float bias = 0.0002;
            shadow = currentDepth - bias > closestDepth ? 1.0 : 0.0;
        }
        return shadow;
    }
    float luminanceFromColor(vec3 color)
    {
        //standard luminance coefficients
        return dot(color, vec3(0.2126f, 0.7152f, 0.0722f));
    }
    //https://64.github.io/tonemapping/
    //Interpolates between per-channel reinhard and luninance-based reinhard
    vec3 reinhardJodie(vec3 color)
    {
        float luminance = luminanceFromColor(color);
        vec3 reinhardPerChannel = color / (1.0f + color);
        vec3 reinhardLuminance = color / (1.0f + luminance);
        return mix(reinhardLuminance, reinhardPerChannel, reinhardPerChannel);
    }
    void main() {

        #ifdef WIREFRAME
        // OGL doesn't have a wireframe rasterizer fill mode like DX, so we'll use barycentric coords instead
        float edgeWidth = 0.005;
        if( v_baryCoord.x < edgeWidth||v_baryCoord.y < edgeWidth||v_baryCoord.z < edgeWidth){
            fragmentColor = vec4(1.0, 1.0, 1.0, 1.0);
            return;
        }
        else
            discard;
        #endif

        // We're doing light calculations in world space
        vec3 viewDir = normalize(u_camPosWorldSpace.xyz - v_fragPos.xyz); // world-space

        //Parallax occlusion mapping. WIP.
        float height = 0.0;
        vec2 uv = v_texCoord;
        uv *= u_textureScale;
        #ifdef USE_PARALLAX
        vec3 uvh = getContactRefinementParallaxCoordsAndHeight(uv, viewDir);
        uv = uvh.xy;
        height = uvh.z;
        #endif

        vec4 baseColor = texture(u_baseColorTexture, uv);
        #ifdef USE_PBR
        baseColor *= u_baseColorFactor;
        #endif
        #ifndef SKIP_LIGHTING
            // If normal mapping, transform tangent space normal
            // to view space using TBN matrix. Else, just normalize v_normal.
            #ifdef USE_NORMAL_MAP
                vec3 textureNormal = texture(u_normalMap, uv).rgb;
                #ifdef INVERT_NORMAL_MAP
                    textureNormal.g = 1.0-textureNormal.g;     
                #endif
                vec3 tangentSpaceNormal = normalize(textureNormal * 2.0 - 1.0);
                vec3 normal = normalize(m_TBN * tangentSpaceNormal);
            #else
                vec3 normal = v_normal;
            #endif

            //fragmentColor = vec4(normal, 1.0);
            //return;
            // Set up pbr values, if we are using it
            float metallic = 0.0;
            float roughness = 0.0;
            vec3 F0 = vec3(0.04); // Total hack, this should be specified per-material
            #ifdef USE_PBR
                #ifndef PBR_MAPS
                    metallic = u_metallicFactor;
                    roughness = u_roughnessFactor;
                #else
                    #ifdef COMBINED_METALLIC_ROUGHNESS
                        vec2 metallicRoughness = texture(u_metallicRoughness, uv).gb; // GLTF spec
                        metallic = metallicRoughness.y*u_metallicFactor;
                        roughness = metallicRoughness.x*u_metallicFactor;
                    #else
                        metallic = texture(u_metallic, uv).r;
                        roughness = texture(u_roughness, uv).r;
                    #endif
                #endif
            #endif
            F0 = mix(F0, baseColor.xyz, metallic);

            // Accumulate light from all lights. WIP.
            vec3 lighting = vec3(0.0, 0.0, 0.0);
            float normalOffsetBias = 0.05;
            vec4 fragPosWorldSpace = vec4(v_fragPos.xyz+normal*normalOffsetBias, v_fragPos.w);
            int dirLightNum = 0;
            int spotLightNum = 0;
            int pointLightNum = 0;
            for(int i = 0; i < u_numLights; i++) {
                float shadow = 0.0;
                int lightType = int(round(u_lightProperties[i].x));
                switch (lightType){
                    case LIGHT_TYPE_DIRECTIONAL:
                        shadow = calculateCascadedShadow(fragPosWorldSpace, dirLightNum, normalize(v_normal), i);
                        dirLightNum++;
                        break;
                    case LIGHT_TYPE_SPOT:
                        shadow = calculateSpotShadow(fragPosWorldSpace, spotLightNum);
                        spotLightNum++;
                        break;
                    case LIGHT_TYPE_POINT:
                        shadow = calculatePointShadow(fragPosWorldSpace, pointLightNum, i);
                        pointLightNum++;
                        break;
                }
                lighting += (1.0-shadow) * calculateLightContribution(i, v_fragPos.xyz, viewDir, normal, uv, baseColor.xyz, metallic, roughness, F0, height);
           }


            // Ambient lighting, use AO map here if we have one
            #ifdef USE_BAKED_AO
                vec3 ambient = u_ambientStrength * baseColor.xyz * texture(u_aoMap, uv).r * AMBIENT_SCALING_FACTOR;
                // color.xyz *= aoColor.r;
            #else
                vec3 ambient = u_ambientStrength * baseColor.xyz * AMBIENT_SCALING_FACTOR;
            #endif  
            lighting += ambient;
        #else
            float lighting = u_ambientStrength;
        #endif
        vec3 color = baseColor.xyz * lighting;
        // Reinhard tonemapping
        color = reinhardJodie(color);
        #ifdef USE_PBR
        // Gamma correction
        color = pow(color, vec3(1.0 / 2.2));
        #endif

        // Apply opacity
        float opacity = baseColor.a;
        #ifdef USE_OPACITY_MAP
        opacity = 1.0 - texture(u_opacity, uv).r;
        #endif
        fragmentColor = vec4(color, opacity);
    }
</script>
<script id="shadowVSSource" type="x-shader/x-vertex">#version 300 es
    precision mediump float;
    in vec3 a_position;
    //attribute vec2 a_texCoord;

    uniform mat4 u_modelViewMatrix;
    uniform mat4 u_projectionMatrix;

    //varying vec3 v_fragPos;
    //varying vec3 v_texCoord;

    void main() {
        // Transform the position into view space
        vec3 v_fragPos = vec3(u_modelViewMatrix * vec4(a_position, 1.0));

        //pass texcoord to fs
        //v_texCoord = a_texCoord;

        // Set the position
        gl_Position = u_projectionMatrix * vec4(v_fragPos, 1.0);
    }
</script>
<script id="shadowFSSource" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    layout(location = 0) out float fragmentdepth;

    void main() {
        fragmentdepth = gl_FragCoord.z;
    }

</script>
<body>
    <canvas id="webgl-canvas" width="800" height="800"></canvas>
    <script src="gl-matrix-min.js"></script>
    <script type="text/javascript" src="globals.js"></script>
    <script type="text/javascript" src="types.js"></script>
    <script type="text/javascript" src="utils.js"></script>
    <script type="text/javascript" src="debug_quad.js"></script>
    <script type="text/javascript" src="debug_triangle.js"></script>
    <script type="text/javascript" src="models.js"></script>
    <script type="text/javascript" src="renderer.js"></script>
    <script type="text/javascript" src="renderer_shadow_funcs.js"></script>
    <script type="text/javascript" src="renderer_lines.js"></script>
    <script type="text/javascript" src="main.js"></script>
</body>
</html>